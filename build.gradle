plugins {
    id "io.freefair.lombok" version "3.5.1"
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'pl.decerto'
version = '0.1-SNAPSHOT'
sourceCompatibility = '11'

springBoot {
    buildInfo()
}

repositories {
    mavenCentral()
}


configurations {
    jaxb
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated/sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "src/main/resources/dictionaries.xsd"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir, schema: schema) {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }

            javac(destdir: classesDir, debug: true, debugLevel: "lines,vars,source",
                    classpath: configurations.jaxb.asPath, includeantruntime: false) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'wsdl4j:wsdl4j:1.6.3'
    implementation 'com.h2database:h2:1.4.196'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'pl.decerto:hyperon-runtime:1.6.51'
    implementation('org.glassfish.jaxb:jaxb-runtime:2.3.2')
    jaxb("org.glassfish.jaxb:jaxb-xjc:2.3.2")
    implementation(files(genJaxb.classesDir).builtBy(genJaxb))

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
